# Stolen from https://github.com/Mic92/dotfiles/blob/63ad2fe59b9e48939b7593844fb660ab0d07f875/nixosModules/fhs-compat.nix
# Copyright © 2021 Jörg Thalheim
# MIT License

{
  pkgs,
  lib,
  config,
  ...
}:
with lib;
let
  cfg = config.lh.system.fhs-compat;
in
{
  options.lh.system.fhs-compat = {
    enable = lib.mkEnableOption "Enable FHS compatibility layer (envfs and nix-ld).";

    enableHardcodedBinFix = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = ''
        Whether to Mount /bin, /usr/bin, /sbin, /usr/sbin into envfs to
        support programs with hardcoded paths.
      '';
    };
  };

  config = mkIf cfg.enable {

    services.envfs.enable = cfg.enableHardcodedBinFix;
    programs.nix-ld = {

      enable = lib.mkDefault true;
      libraries =
        with pkgs;
        [
          acl
          attr
          bzip2
          dbus
          expat
          fontconfig
          freetype
          fuse3
          icu
          libnotify
          libsodium
          libssh
          libunwind
          libusb1
          libuuid
          nspr
          nss
          stdenv.cc.cc
          util-linux
          zlib
          zstd
        ]
        ++ lib.optionals config.hardware.graphics.enable [
          pipewire
          cups
          libxkbcommon
          pango
          mesa
          libdrm
          libglvnd
          libpulseaudio
          atk
          cairo
          alsa-lib
          at-spi2-atk
          at-spi2-core
          gdk-pixbuf
          glib
          gtk3
          libGL
          libappindicator-gtk3
          vulkan-loader
          xorg.libX11
          xorg.libXScrnSaver
          xorg.libXcomposite
          xorg.libXcursor
          xorg.libXdamage
          xorg.libXext
          xorg.libXfixes
          xorg.libXi
          xorg.libXrandr
          xorg.libXrender
          xorg.libXtst
          xorg.libxcb
          xorg.libxkbfile
          xorg.libxshmfence
          xorg.libICE
          xorg.libSM
        ];
    };
  };
}
